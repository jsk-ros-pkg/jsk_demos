(load "package://drc_task_common/euslisp/robot-util.l")
(load "package://drc_task_common/euslisp/util.l")

(defun set-valve-environment
  (&key (valve-r 200)
        (valve-coords (make-coords :pos (float-vector 0 0 1200) :rpy (list 0 pi/2 0)))
        (generate-ri? t) (draw? t)
        (arm :rarm)
        (robot-name "HRP2JSKNTS"))
  ;; generate robot
  (generate-robot robot-name :generate-ri? generate-ri?)
  ;; place robot to origin
  (send *robot* :reset-manip-pose)
  (send *robot* :fix-leg-to-coords (make-coords))
  (setq *valve-arm* arm)
  ;; attach handle to robot
  (let* (larm-valve-translate-offset
         rarm-valve-translate-offset
         larm-valve-z-rotate-offset
         rarm-valve-z-rotate-offset)
    ;; handle for valve edge
    (cond ((or (equal *robot-name* "JAXON") (equal *robot-name* "STARO"))
           (setq larm-valve-translate-offset (float-vector -10.0 0.0 0.0))
           (setq rarm-valve-translate-offset (float-vector -10.0 0.0 0.0))
           (setq larm-valve-z-rotate-offset (deg2rad 0))
           (setq rarm-valve-z-rotate-offset (deg2rad -0))
           )
          (t ;; (or (equal *robot-name* "HRP2JSKNT") (equal *robot-name* "HRP2JSKNTS"))
           (setq larm-valve-translate-offset (float-vector 25.0 5.0 0.0))
           (setq rarm-valve-translate-offset (float-vector 25.0 -5.0 0.0))
           (setq larm-valve-z-rotate-offset 0)
           (setq rarm-valve-z-rotate-offset 0)))
    (send *robot* :put :larm-valve-coords
          (make-cascoords
           :coords (send (send (send (send (send *robot* :larm :end-coords) :copy-worldcoords) :translate larm-valve-translate-offset :local) :rotate pi :x :local) :rotate larm-valve-z-rotate-offset :z :local)
           :parent (send (send *robot* :larm :end-coords) :parent)))
    (send *robot* :put :rarm-valve-coords
          (make-cascoords
           :coords (send (send (send (send *robot* :rarm :end-coords) :copy-worldcoords) :translate rarm-valve-translate-offset :local) :rotate rarm-valve-z-rotate-offset :z :local)
           :parent (send (send *robot* :rarm :end-coords) :parent)))
    ;; handle for valve center
    (cond ((or (equal *robot-name* "JAXON") (equal *robot-name* "STARO"))
           (setq larm-valve-translate-offset (float-vector -10.0 0.0 0.0))
           (setq rarm-valve-translate-offset (float-vector -10.0 0.0 0.0))
           (setq larm-valve-z-rotate-offset (deg2rad 0))
           (setq rarm-valve-z-rotate-offset (deg2rad -0))
           )
          (t ;; (or (equal *robot-name* "HRP2JSKNT") (equal *robot-name* "HRP2JSKNTS"))
           (setq larm-valve-translate-offset (float-vector 40.0 -15.0 0.0))
           (setq rarm-valve-translate-offset (float-vector 40.0 15.0 0.0))
           (setq larm-valve-z-rotate-offset (deg2rad -55))
           (setq rarm-valve-z-rotate-offset (deg2rad 55))))
    (send *robot* :put :larm-valve-coords-for-center
          (make-cascoords
           :coords (send (send (send (send *robot* :larm :end-coords) :copy-worldcoords) :translate larm-valve-translate-offset :local) :rotate larm-valve-z-rotate-offset :z :local)
           :parent (send (send *robot* :larm :end-coords) :parent)))
    (send *robot* :put :rarm-valve-coords-for-center
          (make-cascoords
           :coords (send (send (send (send *robot* :rarm :end-coords) :copy-worldcoords) :translate rarm-valve-translate-offset :local) :rotate rarm-valve-z-rotate-offset :z :local)
           :parent (send (send *robot* :rarm :end-coords) :parent)))
    )
  ;; generate valve
  (let* ((valve-sr (* 0.1 valve-r)))
    (setq *valve* (make-ring valve-r valve-sr :segments 16))
    (send *valve* :set-color (float-vector 1 1 0))
    )
  ;; atattch handle to valve
  (send *valve* :put :grasp-coords
        (make-cascoords
         :coords (make-coords :pos (float-vector valve-r 0 0) :rpy (list pi/2 -pi/2 0))
         :parent *valve*))
  (send *valve* :put :center-grasp-coords
        (make-cascoords
         :coords (make-coords :pos (float-vector 0 0 0) :rpy (list pi/2 -pi/2 0))
         :parent *valve*))
  ;; place valve to valve-coords
  (when valve-coords
    (send *valve* :newcoords valve-coords))
  (setq *valve-stand-coords* (make-coords))
  ;; draw
  (setq *ground* (make-cube 1000 1000 1))
  (when (or draw? (boundp '*irtviewer*))
    (objects (list *robot* *valve* *ground*)))
  (when draw?
    (send *irtviewer* :draw-objects)
    (send *irtviewer* :change-background (float-vector 1 1 1))
    )
  )

(defun set-valve-environment-full
  (valve-r
   valve-coords ;; body relative
   &key
   (arm :rarm)
   (stand-coords nil)
   (draw? nil)
   (use-ri? t))
  (let* (valve-worldcoords)
    ;; place robot and valve to initial coords
    (set-valve-environment :valve-r valve-r :valve-coords nil :arm arm :draw? draw? :generate-ri? use-ri?)
    (cond (use-ri?
           (send *robot* :angle-vector (send *ri* :state :potentio-vector)))
          (t
           (send *robot* :angle-vector (get-potentio-vector-from-joint-states))))
    (send *robot* :fix-leg-to-coords (make-coords))
    (send *valve* :newcoords (send (send (send *robot* :body_lk) :copy-worldcoords) :transform valve-coords :local))
    (setq *valve-stand-coords* (if stand-coords stand-coords (make-coords)))
    (send *robot* :fix-leg-to-coords *valve-stand-coords*)
    (when draw? (send *irtviewer* :draw-objects))
    ))

(defun reset-valve-angle
  ()
  (let* ((handle-height-min 1e10)
         valve-rot-min)
    (dotimes (i 360)
      (send *valve* :rotate (deg2rad 1) :z :local)
      (when (> handle-height-min (elt (send (send *valve* :get :grasp-coords) :worldpos) 2))
        (setq handle-height-min (elt (send (send *valve* :get :grasp-coords) :worldpos) 2))
        (setq valve-rot-min (send (send (send *valve* :worldcoords) :copy-worldcoords) :worldrot))
        )
      )
    (send *valve* :newcoords (make-coords :pos (send (send *valve* :worldcoords) :worldpos) :rot valve-rot-min))
    )
  )


(defun fullbody-inverse-kinematics-for-valve
  (tc mt ll &key (draw? nil))
  (cond ((or (equal *robot-name* "JAXON") (equal *robot-name* "STARO"))
         (fullbody-inverse-kinematics-for-valve-for-jaxon tc mt ll :draw? draw?))
        (t ;; (or (equal *robot-name* "HRP2JSKNT") (equal *robot-name* "HRP2JSKNTS"))
         (fullbody-inverse-kinematics-for-valve-for-hrp2 tc mt ll :draw? draw?))
        ))

(defun fullbody-inverse-kinematics-for-valve-for-hrp2
  (tc mt ll &key (draw? nil))
  (send *robot*
        :fullbody-inverse-kinematics tc
        :move-target mt :link-list ll
        :root-link-virtual-joint-weight (float-vector 0.1 0.1 0.1 0.1 0.5 0.5)
        :target-centroid-pos (apply #'midpoint 0.5 (send *robot* :legs :end-coords :worldpos))
        :translation-axis (list t t t)
        :rotation-axis (list t t t)
        :warnp (if draw? t nil)
        :additional-weight-list
        (list (list (send *robot* :rleg :toe-p :child-link) 0)
              (list (send *robot* :lleg :toe-p :child-link) 0))
        :max (float-vector  500  500  0  20  20  10)
        :min (float-vector  -500  -500  -200  -20  -20  -10)
        :stop 20
        :debug-view (if draw? :no-message nil)
        ))

(defun fullbody-inverse-kinematics-for-valve-for-jaxon
  (tc mt ll &key (draw? nil))
  (let (ret)
    (setq ret
          (send *robot*
                :fullbody-inverse-kinematics tc
                :move-target mt :link-list ll
                :target-centroid-pos (apply #'midpoint 0.5 (send *robot* :legs :end-coords :worldpos))
                :translation-axis (list t t t)
                :rotation-axis (list t t t)
                :warnp (if draw? t nil)
                :root-link-virtual-joint-weight (float-vector 0.15 0.15 0.01 0.15 0.5 0.5)
                :additional-weight-list
                (list (list (send *robot* :torso :waist-r :child-link) 0.1)
                      (list (send *robot* :torso :waist-y :child-link) 0.1)
                      (list (send *robot* :torso :waist-p :child-link) 0.1))
                :max (float-vector  500  200  25  10  20  10)
                :min (float-vector  -500  -200  -200  -10  -20  -10)
                :stop 50
                :debug-view (if draw? :no-message nil)
                :avoid-collision-distance 100
                :avoid-collision-null-gain 5.0
                :avoid-collision-joint-gain 0.8
                :collision-avoidance-link-pair
                (list (list (elt (send *robot* *valve-arm* :links) 2) (send *robot* :link "CHEST_LINK2")))
                ))
    (when (and ret (send *robot* :self-collision-check)) (setq ret nil))
    ret)
  )

(defun generate-valve-motion
  (&key (draw? t) (grasp-center? nil) (ccw? t))
  ;; reset valve angle
  (reset-valve-angle)
  ;; loop
  (let* ((devide-num 10)
         (reach-dist 100)
         (ik-results nil)
	 (valve-handle-name :grasp-coords)
	 )
    (when grasp-center?
      (setq valve-handle-name :center-grasp-coords))
    ;; rotate valve
    (dotimes (i devide-num)
      (send *robot* :reset-manip-pose)
      (send *robot* :fix-leg-to-coords *valve-stand-coords*)
      ;; solve ik to rotate valve
      (let* (ik-ret-in
             ik-ret-out
             tc)
        (when draw? (send (send *valve* :get valve-handle-name) :draw-on :flush t))
        ;; solve ik for valve handle
        (setq tc
              (list
               (send *valve* :get valve-handle-name)
               (send *robot* :rleg :end-coords :copy-worldcoords)
               (send *robot* :lleg :end-coords :copy-worldcoords)))
        (with-move-target-link-list
         (mt ll *robot* (list *valve-arm* :rleg :lleg))
         (setf (elt mt 0) (send *robot* :get (read-from-string (format nil "~a-valve-coords~a" *valve-arm* (if grasp-center? "-for-center" "")))))
         (setq ik-ret-in (fullbody-inverse-kinematics-for-valve tc mt ll :draw? draw?))
         (when ik-ret-in
           (setq ik-ret-in (send *robot* :look-at-target (send *valve* :worldcoords)))))
        ;; solve ik for valve reaching point
        (cond ((or (equal *robot-name* "JAXON") (equal *robot-name* "STARO"))
               (setq tc
                     (list
                      (send (send (send *valve* :get valve-handle-name) :copy-worldcoords) :translate (float-vector (* (- reach-dist) 1.5) 0 0) :local)
                      (send *robot* :rleg :end-coords :copy-worldcoords)
                      (send *robot* :lleg :end-coords :copy-worldcoords))))
              (t ;; (or (equal *robot-name* "HRP2JSKNT") (equal *robot-name* "HRP2JSKNTS"))
               (setq tc
                     (list
                      (cond (grasp-center?
                             (send (send (send *valve* :get valve-handle-name) :copy-worldcoords) :translate (float-vector (- reach-dist) 0 0) :local))
                            (t ;; (not grasp-center?)
                             (send (send (send *valve* :get valve-handle-name) :copy-worldcoords) :translate (float-vector (- reach-dist) (- reach-dist) 0) :local)))
                      (send *robot* :rleg :end-coords :copy-worldcoords)
                      (send *robot* :lleg :end-coords :copy-worldcoords)))))
        (when ik-ret-in
          (with-move-target-link-list
           (mt ll *robot* (list *valve-arm* :rleg :lleg))
           (setf (elt mt 0) (send *robot* :get (read-from-string (format nil "~a-valve-coords~a" *valve-arm* (if grasp-center? "-for-center" "")))))
           (setq ik-ret-out (fullbody-inverse-kinematics-for-valve tc mt ll :draw? draw?))
           (when ik-ret-out
             (setq ik-ret-out (send *robot* :look-at-target (send *valve* :worldcoords))))))
        (push-back (list i (not (null (and ik-ret-in ik-ret-out))) ik-ret-in ik-ret-out) ik-results)
        ;; draw
        (when draw? (send *irtviewer* :draw-objects :flush t))
        )
      (setq *valve-ik-results* ik-results)
      ;; rotate valve
      (send *valve* :rotate (* (if ccw? -1 1) (deg2rad (/ 360.0 devide-num))) :z :local)
      )
    ))

(defun search-rotatable-range
  (&key (arm *valve-arm*) (check-continuous? t))
  (let* ((ik-results-double (append *valve-ik-results* *valve-ik-results*))
         (test-point 0)
         (rotatable-point nil))
    (dotimes (i (/ (length ik-results-double) 2))
      (let (j)
        (setq test-point 0)
        (setq j (+ i 1))
        (when (car (cdr (nth i ik-results-double)))
          (incf test-point)
          (while (and
                  (car (cdr (nth j ik-results-double)))
                  (or (not check-continuous?)
                      (check-angle-vector-continuous (elt (elt ik-results-double (1- j)) 3) (elt (elt ik-results-double j) 3) arm)
                      ;; (and
                      ;;  (check-angle-vector-continuous (elt (elt ik-results-double (1- j)) 2) (elt (elt ik-results-double j) 2) arm)
                      ;;  (check-angle-vector-continuous (elt (elt ik-results-double (1- j)) 3) (elt (elt ik-results-double j) 3) arm))
                      ))
            (incf test-point)
            (incf j)))
        (push-back test-point rotatable-point)
        ))
    rotatable-point
    ))

(defun get-rotatable-angle
  ()
  (* 360 (/ (float (apply #'max (search-rotatable-range))) (length *valve-ik-results*))))

(defun get-valve-motion
  (&key (draw? nil) (grasp-center? nil))
  (generate-valve-motion :draw? draw? :grasp-center? grasp-center?)
  (let* ((valve-rotatable-range (search-rotatable-range))
         (rotatable-point-max (apply #'max valve-rotatable-range))
         (rotatable-point-idx (position (apply #'max valve-rotatable-range) valve-rotatable-range))
         angle-vector-ret angle-vector-ret-release)
    (dotimes (i rotatable-point-max)
      (push-back (elt (elt *valve-ik-results* rotatable-point-idx) 2) angle-vector-ret)
      (push-back (elt (elt *valve-ik-results* rotatable-point-idx) 3) angle-vector-ret-release)
      (setq rotatable-point-idx (mod (+ rotatable-point-idx 1) (length valve-rotatable-range)))
      )
    ;; (dotimes (i (length valve-rotatable-range))
    ;;   (when (= (elt valve-rotatable-range rotatable-point-idx) 0)
    ;;     (return-from nil nil))
    ;;   (push-back (elt (elt *valve-ik-results* rotatable-point-idx) 2) angle-vector-ret)
    ;;   (push-back (elt (elt *valve-ik-results* rotatable-point-idx) 3) angle-vector-ret-release)
    ;;   (setq rotatable-point-idx (mod (+ rotatable-point-idx 1) (length valve-rotatable-range)))
    ;;   )
    (setq *valve-motion* (append angle-vector-ret (reverse angle-vector-ret-release)))
    *valve-motion*
    ))

(defun get-valve-stand-coords ;; relative to valve
  (&key (grasp-center? nil))
  (cond ((or (equal *robot-name* "JAXON") (equal *robot-name* "STARO"))
         (cond ((equal *valve-arm* :larm)
                (make-coords :pos (float-vector -600 -500 0) :rpy (list (deg2rad 0) 0 0)))
               ((equal *valve-arm* :rarm)
                (make-coords :pos (float-vector -600 500 0) :rpy (list (deg2rad 0) 0 0))))
         )
        (t ;; (or (equal *robot-name* "HRP2JSKNT") (equal *robot-name* "HRP2JSKNTS"))
         (cond (grasp-center?
                (cond ((equal *valve-arm* :larm)
                       (make-coords :pos (float-vector -400 -500 0) :rpy (list (deg2rad 0) 0 0)))
                      ((equal *valve-arm* :rarm)
                       (make-coords :pos (float-vector -400 500 0) :rpy (list (deg2rad 0) 0 0)))))
               (t
                (cond ((equal *valve-arm* :larm)
                       (make-coords :pos (float-vector -500 -120 0) :rpy (list (deg2rad 0) 0 0)))
                      ((equal *valve-arm* :rarm)
                       (make-coords :pos (float-vector -500 120 0) :rpy (list (deg2rad 0) 0 0))))
                ))
         ))
  )

(defun move-valve-with-stand-coords
  (stand-coords)
  (let* ((x-vec (send (send (send *valve* :worldcoords) :copy-worldcoords) :rotate-vector (float-vector 0 0 1)))
         (z-vec (float-vector 0 0 1))
         (y-vec (v* z-vec x-vec))
         (valve-coords-stand-rot (make-coords :pos (send (send *valve* :copy-worldcoords) :worldpos) :rot (transpose (matrix x-vec y-vec z-vec))))
         (tmp-stand-coords (send (send valve-coords-stand-rot :copy-worldcoords) :transform stand-coords :local))
         (stand-pos (send tmp-stand-coords :worldpos))
         (stand-rpy (elt (send tmp-stand-coords :rpy-angle) 0))
         )
    (setq *valve-stand-coords* (make-coords :pos (float-vector (elt stand-pos 0) (elt stand-pos 1) 0) :rpy (list (elt stand-rpy 0) 0 0)))
    )
  )

(defun get-valve-motion-full
  (valve-r
   valve-coords
   &key (arm :rarm) (grasp-center? nil) (stand-coords nil) (set-environment? t) (overwrite-stand-coords? nil) (overwrite-arm-side? nil) (draw? nil) (use-ri? t))
  (let* ((rotatable-angle-thre 100))
    (when set-environment?
      (set-valve-environment-full valve-r valve-coords :arm arm :stand-coords stand-coords :draw? draw? :use-ri? use-ri?))
    ;; first trial
    (get-valve-motion :grasp-center? grasp-center? :draw? draw?)
    ;; change arm
    (when (and overwrite-arm-side? (<= (get-rotatable-angle) rotatable-angle-thre))
      (setq *valve-arm* (get-opposite-arm *valve-arm*))
      (ros::ros-warn "change arm side to ~a for valve motion.~%" *valve-arm*)
      (get-valve-motion :grasp-center? grasp-center? :draw? draw?)
      )
    ;; revert arm, change stand position
    (when (and overwrite-stand-coords? (<= (get-rotatable-angle) rotatable-angle-thre))
      (when overwrite-arm-side? (setq *valve-arm* (get-opposite-arm *valve-arm*)))
      (move-valve-with-stand-coords (get-valve-stand-coords :grasp-center? grasp-center?))
      (ros::ros-warn "change arms side to ~a and stand position to ~a for valve motion.~%" *valve-arm* *valve-stand-coords*)
      (get-valve-motion :grasp-center? grasp-center? :draw? draw?)
      )
    ;; change arm, change stand position
    (when (and overwrite-arm-side? overwrite-stand-coords? (<= (get-rotatable-angle) rotatable-angle-thre))
      (when overwrite-arm-side? (setq *valve-arm* (get-opposite-arm *valve-arm*)))
      (move-valve-with-stand-coords (get-valve-stand-coords :grasp-center? grasp-center?))
      (ros::ros-warn "change arms side to ~a and stand position to ~a for valve motion.~%" *valve-arm* *valve-stand-coords*)
      (get-valve-motion :grasp-center? grasp-center? :draw? draw?)
      )
    *valve-motion*))

(defun send-valve-motion
  (radius coords &key (arm :rarm) (stand-coords nil) (overwrite-stand-coords? nil) (overwrite-arm-side? nil) (real? t) (av-list nil))
  (let* ((rotate-num 3)
         (valve-motion-av-list
          (if av-list
              av-list (get-valve-motion-full radius coords :arm arm :stand-coords stand-coords :overwrite-stand-coords? overwrite-stand-coords? :overwrite-arm-side? overwrite-arm-side?)))
         (valve-motion-length (length valve-motion-av-list)))
    (when (not (= valve-motion-length 0))
      ;; reset-manip-pose
      (hand-stop-grasp :real? real?)
      (send *robot* :reset-manip-pose)
      (unless (check-angle-vector-distance)
        (when real? (model2real :time 3000)))
      (when real?
        (cond ((or (equal *robot-name* "JAXON") (equal *robot-name* "STARO"))
               (send *ri* :start-impedance :arms :moment-gain (float-vector 0 0 0) :k-p 300 :d-p 300))
              (t ;; (or (equal *robot-name* "HRP2JSKNT") (equal *robot-name* "HRP2JSKNTS"))
               (send *ri* :start-impedance :arms :moment-gain (float-vector 0 0 0) :k-p 800 :d-p 400))))
      ;; walk
      (when *valve-stand-coords*
        (let* ((pos (scale 0.001 (send *valve-stand-coords* :worldpos)))
               (rpy (mapcar #'rad2deg (car (send *valve-stand-coords* :rpy-angle))))
               (pos-thre 0.01)
               (rpy-thre 1)
               )
          (when (or (> (abs (elt pos 0)) pos-thre) (> (abs (elt pos 1)) pos-thre) (> (abs (elt rpy 0)) rpy-thre))
            (ros::ros-warn "go-pos to ~a ~a ~a for valve~%" (elt pos 0) (elt pos 1) (elt rpy 0))
            (when real? (send *ri* :go-pos (elt pos 0) (elt pos 1) (elt rpy 0)))
            )
          ))
      ;; pre grasp
      (send *robot* :angle-vector (car (last valve-motion-av-list)))
      (when real? (model2real :time 5000))
      (dotimes (i rotate-num)
        ;; grasp
        (send *robot* :angle-vector (car valve-motion-av-list))
        (when real? (model2real :time 3000))
        (hand-start-grasp :real? real?)
        ;; rotate
        (when real? (send-angle-vector-sequence-with-time (cdr (subseq valve-motion-av-list 0 (/ valve-motion-length 2))) :time 1500 :wait? t))
        ;; relese
        (hand-stop-grasp :real? real?)
        (send *robot* :angle-vector (car (subseq valve-motion-av-list (/ valve-motion-length 2))))
        (when real? (model2real :time 3000))
        (cond ((= i (1- rotate-num))
               ;; last releasing
               (send *robot* :reset-manip-pose)
               (when real? (model2real :time 5000)))
              (t
               ;; reverse
               (when real? (send-angle-vector-sequence-with-time (cdr (subseq valve-motion-av-list (/ valve-motion-length 2))) :time 1500 :wait? t))
               ))
        )))
  )

(defun publish-valve-motion
  (&key radius coords (arm :rarm) (stand-coords nil) (overwrite-stand-coords? nil) (overwrite-arm-side? nil) (step-time 200) (update? t))
  (when update?
    (get-valve-motion-full radius coords :arm arm :stand-coords stand-coords :overwrite-stand-coords? overwrite-stand-coords? :overwrite-arm-side? overwrite-arm-side? :use-ri? nil))
  (ros::ros-info "valve-stand-coords: ~a~%" *valve-stand-coords*)
  (ros::ros-info "valve-ik-results: ~a~%" *valve-ik-results*)
  (when *valve-stand-coords*
    (move-robot-model *valve-stand-coords* "ground"))
  (publish-motion-joint-states *valve-motion* :step-time step-time)
  )

;; sample

(defun test-valve-motion
  (&key (robot-name "HRP2JSKNT")
        (x-pos 500) (y-pos -120) (z-pos 1260) (yaw-angle 0)
        (grasp-center? nil) (arm :rarm)
        (overwrite-stand-coords? nil) (overwrite-arm-side? nil))
  (set-valve-environment :generate-ri? nil :robot-name robot-name
                         :valve-r (/ 360 2)
                         :valve-coords (make-coords :pos (float-vector x-pos y-pos z-pos) :rpy (list yaw-angle pi/2 0))
                         :arm arm)
  (get-valve-motion-full 0 0 :set-environment? nil :grasp-center? grasp-center? :overwrite-stand-coords? overwrite-stand-coords? :overwrite-arm-side? overwrite-arm-side?)
  (play-motion-on-eus-viewer *valve-motion* :interpolation? t)
  )

(defun test-valve-motion-hrp2jsknt-pattern1
  ()
  (test-valve-motion :x-pos 500 :y-pos -120 :z-pos 1260 :arm :rarm)
  )

(defun test-valve-motion-hrp2jsknt-pattern2
  ()
  (test-valve-motion :x-pos 500 :y-pos 120 :z-pos 1260 :arm :larm)
  )

(defun test-valve-motion-hrp2jsknt-pattern3
  ()
  (test-valve-motion :x-pos 500 :y-pos 120 :z-pos 1260 :arm :rarm :overwrite-arm-side? t)
  )
(defun test-valve-motion-hrp2jsknt-pattern4
  ()
  (test-valve-motion :x-pos 500 :y-pos 120 :z-pos 1260 :arm :rarm :overwrite-stand-coords? t)
  )

(defun test-valve-motion-hrp2jsknt-pattern5
  ()
  (test-valve-motion :x-pos 400 :y-pos -500 :z-pos 1260 :arm :rarm :grasp-center? t)
  )

(defun test-valve-motion-jaxon-pattern1 ;; for continuous test
  ()
  (test-valve-motion :robot-name "JAXON" :yaw-angle (deg2rad 20))
  ;; (2 1 0 0 4 3 2 1 4 3)
  )

(defun test-valve-motion-jaxon-pattern2 ;; for continuous test
  ()
  (test-valve-motion :robot-name "JAXON" :x-pos 420 :y-pos -600 :z-pos 1240 :yaw-angle (deg2rad 20) :grasp-center? t)
  ;; (0 0 6 5 4 3 2 1 1 0)
  )

(defun test-valve-motion-jaxon-pattern3
  ()
  (test-valve-motion :robot-name "JAXON" :x-pos 420 :y-pos 600 :z-pos 1240 :overwrite-arm-side? t)
  )

(defun test-valve-motion-jaxon-pattern4
  ()
  (test-valve-motion :robot-name "JAXON" :x-pos 420 :y-pos 600 :z-pos 1240 :overwrite-stand-coords? t)
  )
